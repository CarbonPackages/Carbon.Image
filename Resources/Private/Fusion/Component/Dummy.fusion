prototype(Carbon.Image:Dummy) < prototype(Neos.Fusion:Component) {
    image = 'resource://Carbon.Image/Public/dummy-image.svg'
    ratio = null
    color = null
    style = Neos.Fusion:DataStructure
    default = ${Configuration.setting('Carbon.Image.dummy.image')}
    preset = null
    width = null
    height = null

    writePadding = true

    @if.hasNoAsset = ${!this.asset}
    renderer = Neos.Fusion:Join {
        styles = Neos.Fusion:Loop {
            items = ${Array.concat(props.default, props.style)}
            itemRenderer = ${itemKey + ':' + item + ';'}
        }
        color = ${'background-color:' + (props.color || Configuration.setting('Carbon.Image.dummy.color')) + ';'}
        image = Neos.Fusion:ResourceUri {
            @if.render = ${props.image}
            path = ${props.image}
            @process.addCSS = ${'background-image:url(' + value + ');'}
        }
        padding = Neos.Fusion:Value {
            @if.render = ${props.writePadding}
            preset = ${props.preset ? Configuration.setting('Neos.Media.thumbnailPresets')[props.preset] : null}
            width = ${this.preset ? this.preset.width : props.width}
            height = ${this.preset ? this.preset.height : props.height}
            ratio = ${this.width && this.height ? this.width / this.height : null}
            padding = ${100 / (props.ratio || this.ratio || Configuration.setting('Carbon.Image.dummy.ratio'))}
            value = ${this.padding ? ('padding-top:' + this.padding + '%') : null}
        }
    }
}

