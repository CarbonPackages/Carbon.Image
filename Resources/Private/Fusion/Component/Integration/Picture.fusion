prototype(Carbon.Image:Component.Picture) < prototype(Neos.Fusion:Component) {
    @propTypes {
        asset = ${PropTypes.anyOf(PropTypes.instanceOf('Neos\Media\Domain\Model\ImageInterface'), PropTypes.string)}
        renderInBackend = ${PropTypes.boolean}
        outputDummy = ${PropTypes.boolean}
        caption = ${PropTypes.string}
        image = ${PropTypes.anyOf(PropTypes.instanceOf('Neos\Media\Domain\Model\ImageInterface'), PropTypes.string)}
        imageSourceSettings = ${PropTypes.arrayOf(PropTypes.any)}
        link = ${PropTypes.anyOf(PropTypes.instanceOf('Neos\ContentRepository\Domain\Model\Node'), PropTypes.string)}
        linkOptions = ${PropTypes.arrayOf(PropTypes.any)}
        linkAdditonalAttributes = ${PropTypes.arrayOf(PropTypes.any)}
        lightbox = ${PropTypes.boolean}
        lightboxConfiguration = ${PropTypes.arrayOf(PropTypes.any)}
        lightboxCaption = ${PropTypes.string}
        linkAttributes = ${PropTypes.any}
    }

    // IMPORTANT!
    // You can pass any value from Carbon.Image:Component.Presentation.Picture
    // To keep it a bit shorter these are not listed

    // asset, assetLightbox, caption and renderInBackend are for easier migration from older versions
    // asset => image
    // assetLightbox => lightboxImage
    // caption => lightboxCaption
    // renderInBackend => outputDummy
    asset = null
    assetLightbox = null
    caption = null
    renderInBackend = ${node.context.inBackend}

    outputDummy = ${this.renderInBackend}

    image = ${this.asset}
    // You can pass all properties from Carbon.Image:Helper.ImageSource
    imageSourceSettings = Neos.Fusion:DataStructure


    // Pass a node or a string. The link gets processed with ConvertUris.
    // It will have no effect if linkAttributes is set.
    link = null
    // Pass any option from ConvertUris or NodeUri
    linkOptions = Carbon.Image:Helper.Link.Options
    // Pass any additional attributes to the link
    // You can even override href
    linkAdditonalAttributes = Neos.Fusion:DataStructure


    // If set, the link for the lightbox gets generated.
    // It will have no effect if link or linkAttributes is set.
    lightbox = null
    lightboxConfiguration = ${Configuration.setting('Carbon.Image.lightbox')}
    // Override the lightbox image
    lightboxImage = ${this.assetLightbox || this.image}
    lightboxCaption = ${this.caption}

    // If you set linkAttributes, this will override lightbox and link
    // linkAttributes = null


    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        rootLinkAttributesIsSet = ${!!props.linkAttributes}
        imageSource = afx`<Carbon.Image:Helper.ImageSource image={props.image} outputDummy={props.outputDummy} {...props.imageSourceSettings} />`
        lightboxImageSource = afx`<Carbon.Image:Helper.LightboxImageSource image={props.lightboxImage} configuration={props.lightboxConfiguration} />`
        alternativeText = Carbon.Image:Helper.AlternativeText {
            asset = ${props.image}
        }

        @if.hasImageSource = ${this.imageSource}

        renderer = Carbon.Image:Component.Presentation.Picture {
            @apply.props = ${props}
            linkAttributes = afx`<Carbon.Image:Helper.Link.Attributes {...props} />`
            renderer.content.wrapper.@process.convertUris = Neos.Neos:ConvertUris {
                @if.set = ${!props.rootLinkAttributesIsSet && props.link}
                @apply.props = ${props.linkOptions}
            }
        }
    }
}
