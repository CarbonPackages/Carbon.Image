prototype(Carbon.Image:Component.Fragment.Image) < prototype(Neos.Fusion:Component) {
    lazy = null
    lazyWidth = null

    id = null
    class = null
    style = null
    title = null
    alt = null

    imageSource = null
    sizes = null
    srcset = null
    objectFit = null

    @if.hasImageSource = ${this.imageSource}

    renderer = afx`
        <img
            id={props.id || null}
            class={props.class || null}
            style={props.style || null}
            title={props.title || null}
            alt={props.alt || null}
            width={props.imageSource.currentWidth}
            height={props.imageSource.currentHeight}
            sizes={props.sizes || null}
            sizes.@process.join={Type.isArray(value) ? Array.join(value, ', ') : value}
            src={props.lazy ? (props.lazyWidth ? props.imageSource.setWidth(props.lazyWidth, true) : null) : props.imageSource}
            srcset={!props.lazy && props.srcset ? props.imageSource.srcset(props.srcset) : null}
            data-src={props.lazy ? props.imageSource : null}
            data-srcset={props.lazy && props.srcset ? props.imageSource.srcset(props.srcset) : null}
            data-sizes={props.lazy && !props.sizes ? 'auto' : null}
            data-aspectratio={props.lazy ? props.imageSource.currentWidth / props.imageSource.currentHeight : null}
            data-parent-fit={props.lazy && props.objectFit}
        />
    `
}
