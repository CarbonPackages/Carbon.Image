prototype(Carbon.Image:Component.Fragment.Image) < prototype(Neos.Fusion:Component) {
    lazy = ${null}
    lazyWidth = ${null}

    id = ${null}
    class = ${null}
    style = ${null}
    title = ${null}
    alt = ${null}
    loading = ${null}

    imageSource = ${null}
    sizes = ${null}
    srcset = ${null}
    objectFit = ${null}

    aspectratio = ${this.lazy && this._width && this._height ? this._width / this._height : null}
    placeholder = 'data:image/gif;base64,R0lGODlhAQABAAAAACH5BAEKAAEALAAAAAABAAEAAAICTAEAOw=='

    // Internal
    _width = ${this.imageSource.width()}
    _height = ${this.imageSource.height()}

    @if.hasImageSource = ${this.imageSource}

    renderer = afx`
        <img
            id={props.id || null}
            class={props.class || null}
            style={props.style || null}
            title={props.title || null}
            loading={props.loading || null}
            alt={props.alt || ''}
            width={props._width}
            height={props._height}
            sizes={props.sizes || null}
            sizes.@process.join={Type.isArray(value) ? Array.join(value, ', ') : value}
            src={props.lazy ? (props.lazyWidth ? props.imageSource.withWidth(props.lazyWidth, true) : props.placeholder) : props.imageSource}
            srcset={!props.lazy && props.srcset ? props.imageSource.srcset(props.srcset) : null}
            data-src={props.lazy ? props.imageSource : null}
            data-srcset={props.lazy && props.srcset ? props.imageSource.srcset(props.srcset) : null}
            data-sizes={props.lazy && !props.sizes ? 'auto' : null}
            data-aspectratio={props.aspectratio}
            data-parent-fit={props.lazy && props.objectFit}
        />
    `
}
