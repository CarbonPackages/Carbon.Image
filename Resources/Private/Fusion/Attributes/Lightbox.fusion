prototype(Carbon.Image:Attributes.Lightbox) < prototype(Neos.Fusion:Component) {
    asset = false
    config = ${Configuration.setting('Carbon.Image.lightbox')}
    class = null
    caption = null
    title = null
    style = null

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        dimensions = afx`
            <Carbon.Image:Internal.GetDimensions asset={props.asset} {...props.config} />
        `

        renderer = Neos.Fusion:Attributes {
            class = Neos.Fusion:DataStructure {
                class = ${props.class}
                config = ${props.config.class}
                @process {
                    filter = ${Carbon.Array.filter(value)}
                    removeEmpty = ${value ? value : false}
                }
            }
            target = ${props.config.target}
            rel = ${props.config.rel}
            data-title = ${props.caption}
            data-size = ${props.dimensions ? props.dimensions.combined : false}
            title = ${props.title}
            style = ${props.style}
            href = afx`
                <Carbon.Image:Attributes.Src asset={props.asset} {...props.config} />
            `
        }
    }
}


